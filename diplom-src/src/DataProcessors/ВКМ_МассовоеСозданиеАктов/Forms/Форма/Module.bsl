
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьАкты(Команда)
	
	Период = Объект.Период;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Выберите период");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СоздатьДокументыНаСервере(); 
	
	Оповещение = Новый ОписаниеОповещения("СоздатьДокументыЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьДокументыНаСервере()
	
	НачалоПериода = НачалоМесяца(Объект.Период);
	КонецПериода = КонецМесяца(Объект.Период);
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
		"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьДокументыНаСервере", НачалоПериода, КонецПериода);

КонецФункции  

&НаКлиенте
Процедура СоздатьДокументыЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат.Статус = "Выполнено" Тогда
		МассивДокументов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ЗаполнитьТабличнуюЧасть(МассивДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧасть(МассивДокументов)

	Объект.ДоговорыИРеализации.Очистить();

	Для Каждого Документ Из МассивДокументов Цикл
		НоваяСтрока = Объект.ДоговорыИРеализации.Добавить();
		НоваяСтрока.Договоры = Документ.Договор;
		НоваяСтрока.РеализацияТоваровУслуг = Документ.Реализация;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
