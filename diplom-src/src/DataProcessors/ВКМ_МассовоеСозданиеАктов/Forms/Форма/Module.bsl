
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВКМ_СоздатьАкты(Команда)
	
	Период = Объект.ВКМ_Период;
	
	Если НЕ ЗначениеЗаполнено(Период) Тогда 
		ОбщегоНазначенияКлиент.СообщитьПользователю("Выберите период");
		Возврат;
	КонецЕсли;
	
	ДлительнаяОперация = СоздатьДокументыНаСервере(); 
	
	Оповещение = Новый ОписаниеОповещения("СоздатьДокументыЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, Оповещение); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СоздатьДокументыНаСервере()
	
	НачалоПериода = НачалоМесяца(Объект.ВКМ_Период);
	КонецПериода = КонецМесяца(Объект.ВКМ_Период);
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, 
		"Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьДокументыНаСервере", НачалоПериода, КонецПериода);

КонецФункции  

&НаКлиенте
Процедура СоздатьДокументыЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат.Статус = "Выполнено" Тогда
		МассивДокументов = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		ЗаполнитьТабличнуюЧасть(МассивДокументов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧасть(МассивДокументов)

	Объект.ВКМ_ДоговорыИРеализации.Очистить();

	Для Каждого Документ Из МассивДокументов Цикл
		НоваяСтрока = Объект.ВКМ_ДоговорыИРеализации.Добавить();
		НоваяСтрока.ВКМ_Договоры = Документ.Договор;
		НоваяСтрока.ВКМ_РеализацияТоваровУслуг = Документ.Реализация;
	КонецЦикла;

КонецПроцедуры

#КонецОбласти
