
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СоздатьДокументыНаСервере(НачалоПериода, КонецПериода) Экспорт

	Результат = ПолучитьДокументы(НачалоПериода, КонецПериода);
	
	МассивРеализаций = Новый Массив;

	Пока Результат.Следующий() Цикл
		РеализацииСтруктура = Новый Структура;
			Если НЕ ЗначениеЗаполнено(Результат.Реализация) Тогда
				РеализацииСтруктура.Вставить("Договор", Результат.Договор);
				РеализацииСтруктура.Вставить("Реализация", СозданиеРеализации(Результат, КонецПериода));
			Иначе
				РеализацииСтруктура.Вставить("Договор", Результат.Договор);
				РеализацииСтруктура.Вставить("Реализация", Результат.Реализация);
			КонецЕсли;

		МассивРеализаций.Добавить(РеализацииСтруктура);

	КонецЦикла;

	Возврат МассивРеализаций;

КонецФункции

Функция ПолучитьДокументы(НачалоПериода, КонецПериода)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка,
	|	ДоговорыКонтрагентов.Организация
	|ПОМЕСТИТЬ ВТ_ДанныеДоговоров
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	&НачалоПериода < ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействияДоговора И &КонецПериода > ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействияДоговора 
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Договор
	|ПОМЕСТИТЬ ВТ_ДанныеРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &НачалоПериода И &КонецПериода
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДоговоров.Ссылка КАК Договор,
	|	ВТ_ДанныеРеализации.Ссылка КАК Реализация,
	|	ВТ_ДанныеДоговоров.Организация,
	|	ВТ_ДанныеДоговоров.Контрагент
	|ИЗ
	|	ВТ_ДанныеДоговоров КАК ВТ_ДанныеДоговоров
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеРеализации КАК ВТ_ДанныеРеализации
	|		ПО ВТ_ДанныеДоговоров.Ссылка = ВТ_ДанныеРеализации.Договор";

	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);

	Результат = Запрос.Выполнить().Выбрать();
	
	Возврат Результат;
	
КонецФункции

Функция СозданиеРеализации(Результат, Дата)
	
	НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	ДанныеЗаполнения = Новый Структура;
	ДанныеЗаполнения.Вставить("Договор", Результат.Договор);
	ДанныеЗаполнения.Вставить("Контрагент", Результат.Контрагент);
	ДанныеЗаполнения.Вставить("Организация", Результат.Организация);
	НовыйДок.Заполнить(ДанныеЗаполнения);
	НовыйДок.Дата = Дата;
	НовыйДок.ВКМ_ВыполнитьАвтозаполнение();
	НовыйДок.ПроверитьЗаполнение();
	НовыйДок.Записать(РежимЗаписиДокумента.Проведение);
	Возврат НовыйДок.Ссылка;

КонецФункции

#КонецОбласти

#КонецЕсли
