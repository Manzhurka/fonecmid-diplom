
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВКМ_Заполнить(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.ВКМ_ДатаНачала) 
		Или Не ЗначениеЗаполнено(Объект.ВКМ_ДатаОкончания) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не заполнен период начисления";
		Сообщение.Сообщить(); 
		Возврат;
	КонецЕсли;
			
	Объект.ВКМ_ОсновныеНачисления.Очистить();

	УИДЗамера = ОценкаПроизводительностиКлиент.ЗамерВремени("ЗаполнениеДокументаНачислениеЗаПервуюПоловинуМесяца",,Ложь);
	
	ЗаполнитьОкладНаСервере(); 
		
	ЗаполнитьПроцентОтРаботНаСервере(); 
			
	ОценкаПроизводительностиКлиент.ЗавершитьЗамерВремени(УИДЗамера);
	УИДЗамера = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьОкладНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Оклад КАК Оклад,
	|	ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник КАК Сотрудник,
	|	Пользователи.Наименование КАК Пользователь
	|ИЗ
	|	Справочник.Пользователи КАК Пользователи
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ВКМ_УсловияОплатыСотрудников.СрезПоследних(&Дата,) КАК
	|			ВКМ_УсловияОплатыСотрудниковСрезПоследних
	|		ПО ВКМ_УсловияОплатыСотрудниковСрезПоследних.ВКМ_Сотрудник = Пользователи.Ссылка
	|ГДЕ
	|	Пользователи.Недействителен = ЛОЖЬ
	|	И Пользователи.Служебный = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("Дата", Объект.ВКМ_ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		//Если Не Выборка.Сотрудник <> Неопределено Тогда
		Если Выборка.Сотрудник = Null	Тогда
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон("Для сотрудника %1 не установлен оклад",
				Выборка.Пользователь));
				Объект.ВКМ_ОсновныеНачисления.Очистить();
			Возврат;
		КонецЕсли;
		НоваяСтрока = Объект.ВКМ_ОсновныеНачисления.Добавить();
		НоваяСтрока.ВКМ_Сотрудник = Выборка.Сотрудник;
		НоваяСтрока.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ОсновныеНачисления.ВКМ_Оклад;
		НоваяСтрока.ВКМ_Показатель = Выборка.Оклад;
		НоваяСтрока.ВКМ_ДатаНачала = Объект.ВКМ_ДатаНачала;
		НоваяСтрока.ВКМ_ДатаОкончания = Объект.ВКМ_ДатаОкончания;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПроцентОтРаботНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник КАК Сотрудник,
	               |	СУММА(ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_СуммаКОплатеОборот) КАК СуммаКОплатеОборот
	               |ИЗ
	               |	РегистрНакопления.ВКМ_ВыполненныеСотрудникомРаботы.Обороты(&НачалоПериода, &КонецПериода, Месяц, ) КАК ВКМ_ВыполненныеСотрудникомРаботыОбороты
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВКМ_ВыполненныеСотрудникомРаботыОбороты.ВКМ_Сотрудник";
	
	Запрос.УстановитьПараметр("НачалоПериода", Объект.ВКМ_ДатаНачала);
	Запрос.УстановитьПараметр("КонецПериода", Объект.ВКМ_ДатаОкончания); 
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.ВКМ_ОсновныеНачисления.Добавить();
		НоваяСтрока.ВКМ_Сотрудник = Выборка.Сотрудник;
		НоваяСтрока.ВКМ_ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ПроцентОтРабот;
		НоваяСтрока.ВКМ_Показатель = Выборка.СуммаКОплатеОборот;
		НоваяСтрока.ВКМ_ДатаНачала = Объект.ВКМ_ДатаНачала;
		НоваяСтрока.ВКМ_ДатаОкончания = Объект.ВКМ_ДатаОкончания;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти
