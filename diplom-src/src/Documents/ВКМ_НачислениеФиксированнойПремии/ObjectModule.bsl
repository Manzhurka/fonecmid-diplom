
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	СформироватьДвижения(); 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьДвижения() 
	
	Движения.ВКМ_ДополнительныеНачисления.Записывать = Истина;
	Движения.ВКМ_Удержания.Записывать = Истина; 
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записывать = Истина;
    
	Для Каждого Строка Из ВКМ_СписокСотрудников Цикл 
		
		//Регистр ВКМ_ДополнительныеНачисления
		Движение = Движения.ВКМ_ДополнительныеНачисления.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_ДополнительныеНачисления.ВКМ_ФиксированнаяПремия;
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.ВКМ_Результат = Строка.ВКМ_СуммаПремии;
		Движение.ПериодРегистрации = Дата;
		
		//Регистр ВКМ_Удержания           	
		Удержание = Строка.ВКМ_СуммаПремии * 13 / 100;
		Движение = Движения.ВКМ_Удержания.Добавить(); 
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.ВидРасчета = ПланыВидовРасчета.ВКМ_Удержания.ВКМ_НДФЛ;
		Движение.ВКМ_Результат = Удержание;
		Движение.ПериодРегистрации = Дата; 
				
		//Регистр ВКМ_ВзаиморасчетыССотрудниками
		Движение = Движения.ВКМ_ВзаиморасчетыССотрудниками.ДобавитьПриход();
		Движение.Период = Дата; 
		Движение.ВКМ_Сотрудник = Строка.ВКМ_Сотрудник;
		Движение.ВКМ_Сумма = Строка.ВКМ_СуммаПремии - Удержание; 
		
	КонецЦикла;                           
	
	Движения.ВКМ_ДополнительныеНачисления.Записать();
	Движения.ВКМ_Удержания.Записать(); 
	Движения.ВКМ_ВзаиморасчетыССотрудниками.Записать();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
