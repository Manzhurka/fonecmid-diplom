
#Область ПрограммныйИнтерфейс

	//Интеграция с телеграм-ботом
Процедура ВКМ_УведомлениеТелеграмБоту() Экспорт
	
	HTTPСоединение = Новый HTTPСоединение("api.telegram.org", 443, , , , , Новый ЗащищенноеСоединениеOpenSSL);

	Ресурс = "/bot" + Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить() + "/sendMessage";
	HTTPЗапрос = Новый HTTPЗапрос(Ресурс);
	HTTPЗапрос.Заголовки.Вставить("Content-Type", "application/json");

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВКМ_УведомленияТелеграмБоту.ВКМ_ТекстСообщения КАК ТекстСообщения,
	|	ВКМ_УведомленияТелеграмБоту.Ссылка
	|ИЗ
	|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
	
		ДанныеОтвета = Новый Структура;
		ДанныеОтвета.Вставить("chat_id", Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить());
		ДанныеОтвета.Вставить("text", Выборка.ТекстСообщения);
	
		СтрокаJSON = СтрокаJSON(ДанныеОтвета);
		HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаJSON);
		
		HTTPОтвет = HTTPСоединение.ВызватьHTTPМетод("POST", HTTPЗапрос);
		
		Если HTTPОтвет.КодСостояния = 200 Тогда
			Выборка.Ссылка.ПолучитьОбъект().Удалить();
		Иначе
			ЗаписьЖурналаРегистрации("HTTPСервисы", УровеньЖурналаРегистрации.Ошибка,,,HTTPОтвет.ПолучитьТелоКакСтроку());
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция СтрокаJSON(ОбъектJSON)

	ПараметрыЗаписи = Новый ПараметрыЗаписиJSON(, Символы.Таб);

	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку(ПараметрыЗаписи);
	ЗаписатьJSON(Запись, ОбъектJSON);

	Возврат Запись.Закрыть();

КонецФункции

#КонецОбласти